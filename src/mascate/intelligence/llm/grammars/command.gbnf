# Command Grammar
# Garante que o LLM produza JSON valido com acoes pre-definidas

root ::= "{" ws "\"action\":" ws action "," ws "\"target\":" ws string params? "}" ws

# Actions whitelist - apenas acoes permitidas
action ::= "\"" ("open_app" | "open_url" | "media_control" | "file_op" | "system_op" | "reply") "\"" ws

# Optional params object
params ::= "," ws "\"params\":" ws object

# Include base primitives
ws ::= [ \t\n]*
string ::= "\"" ([^\\"] | "\\" ([\"\\/bfnrt] | "u" [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F]))* "\"" ws
number ::= ("-"? ([0-9]+) ("." [0-9]+)? ([eE] [-+]? [0-9]+)?) ws
boolean ::= ("true" | "false") ws
null ::= "null" ws

value ::= object | array | string | number | boolean | null

object ::= "{" ws (
            string ":" ws value
            ("," ws string ":" ws value)*
          )? "}" ws

array  ::= "[" ws (
            value
            ("," ws value)*
          )? "]" ws

